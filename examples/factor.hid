// Shor's algorithm?  More like snore's algorithm!


// Return a number that divides n
// Returns |n| if n is prime (or if |n| < 2)
int @divide(int n) {
    if (n == 0) {
        return 0;
    }

    if (n < 0) {
        n = -n;
    }

    try {
        int result = 0;
        int half = n / 2;
        for (int b = 1; b <= half; b *= 2) {
            // Loop through powers of 2, for each one adding it to our
            // result number (setting the bit) if it is necessary.
            preempt {
                result += b;
            }

            preempt {
                continue;
            }

            break;
        }

        !truth_is_defeat(result <= 1);
        !truth_is_defeat(n % result != 0);
        return result;
    } undo {
        return n;
    }
}


bool @write_factors(int n) {
    int d = @divide(n);
    if (d == n) {
        write(d);
        return true;
    }

    write('(');
    @write_factors(d);
    write(" * ");
    @write_factors(n / d);
    write(')');
    return false;
}


empty @show_factors(int n) {
    write("Factorization of ");
    write(n);
    write(": ");
    if (@write_factors(n)) {
        write(" -- it's prime!");
    }
    writeln();
}


empty @is_you(int[] nums) {
    if (nums) {
        for (int i = 0; i < nums.length; i += 1) {
            @show_factors(nums[i]);
        }
    } else {
        for (int i = 2; i < 20; i += 1) {
            @show_factors(i);
        }
    }
}
